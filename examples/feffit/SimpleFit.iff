
macro makepath 1
   "make chi(k) and chi(R) for a single path"
   ff2chi($1, group=path$1)
   fftf(real = path$1.chi)
end macro
macro showpath 1
   "show -|chi(R)| for a path"
   makepath $1
   plot(path$1.r, -path$1.chir_mag, $2)
end macro

read_data(cu_metal_rt.xdi, group=data, label='energy i0 itrans xmu')
spline(data.energy, data.xmu, group=data, rbkg=1.0, kweight=1)

unguess
guess amp    =  1.0
guess de0    = -0.1
guess ss2    =  0.002
guess deltar =  0.001

path(index  = 1,
     feff   = feffcu01.dat,
     label  = "Cu metal first neighbor",
     s02    = amp,
     sigma2 = ss2,
     delr   = deltar,
     e0     = de0)

kmin = 3,  kmax=17, kweight=2, dk=3
rmin = 1.4, rmax =3.0

feffit(chi=data.chi, k=data.k, group=fit, 1)

show @variables, chi_square, r_factor, n_idp, &fit_iteration

newplot data.r, data.chir_mag, xmax=7
plot    fit.r,  fit.chir_mag
# showpath 1  "color=blue"

#
#  amp            =      0.97674559 +/-      0.05061646
#  de0            =      4.17978849 +/-      0.58047603
#  ss2            =      0.00865703 +/-      0.00042329
#  deltar         =     -0.00473801 +/-      0.00347040
#  chi_square     =    2972.330619281
#  r_factor       =       0.004603671
#  n_idp          =      13.895507812
#  &fit_iteration =       6.000000000
#  epsilon_k, epsilon_r =  0.210323383E-03   0.999730083E-02

